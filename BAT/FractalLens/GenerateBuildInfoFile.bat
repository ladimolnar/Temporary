@ECHO OFF
IF "%useverbose%"=="1" ECHO ON
REM =====================================================================
REM FILE: GenerateBuildInfoFile.bat
REM This is the utility that generate a C# file tat contains constants for build info
REM =====================================================================

SET GBF_PathToScriptFolder=%~dp0
CALL %GBF_PathToScriptFolder%\SetCoreEnvVars.bat
IF ERRORLEVEL 1 GOTO LblError

REM GetBuildInfo.bat is only used for SVN Repositories
REM CALL %GBF_PathToScriptFolder%\GetBuildInfo.bat
REM IF ERRORLEVEL 1 GOTO LblError

SET GBF_ExitCode=0

SET GBF_ProjectName=%~1

IF "%GBF_ProjectName%"=="" (
   ECHO Invalid command line. Please specify the project name.>&2
   ECHO Example: GenerateBuildInfoFile.bat Fractalia>&2
   GOTO LblError
)

SET GBF_PathToBuildInfoCSFileTemp="%FRCT_Temp%\BuildInfo.cs"
SET GBF_PathToBuildInfoCSFileFinal="%GBF_PathToScriptFolder%..\%GBF_ProjectName%\BuildInfo.cs"

SET GBF_DateTime=%DATE% %TIME%

CALL :GenerateBuildInfoFile
IF ERRORLEVEL 1 GOTO LblError
GOTO LblDone

:LblError
ECHO Error: GenerateBuildInfoFile.bat was unable to generate the build info file: '%GBF_PathToBuildInfoCSFileFinal%'>&2
SET GBF_ExitCode=1

:LblDone
EXIT /B %GBF_ExitCode%

GOTO :EOF


REM ==============================================================
REM Generates the file BuildInfo.cs
REM ==============================================================
:GenerateBuildInfoFile

SET GBF_GenerateBuildInfoFileExitCode=0

ECHO.
ECHO Generating %GBF_PathToBuildInfoCSFileFinal% 
ECHO.
ECHO Major Version: %FRCT_MajorVersion%
ECHO Minor Version: %FRCT_MinorVersion%
ECHO SVN Revision Nr: %FRCT_SvnRevisionNr%
ECHO SVN Open State: %FRCT_SvnOpenState%
ECHO Build DateTime: %GBF_DateTime%

DEL /F /Q %GBF_PathToBuildInfoCSFileTemp% >nul 2>&1
IF EXIST %GBF_PathToBuildInfoCSFileTemp% (
   ECHO Error: Unable to delete %GBF_PathToBuildInfoCSFileTemp% >&2
   GOTO LblGenerateBuildInfoFileError
)

ECHO // File auto generated by the build system. See GenerateBuildInfoFile.bat> %GBF_PathToBuildInfoCSFileTemp%
ECHO // Do not add to SVN. Do not change manually!>> %GBF_PathToBuildInfoCSFileTemp%
ECHO.>> %GBF_PathToBuildInfoCSFileTemp%
ECHO using System;>> %GBF_PathToBuildInfoCSFileTemp%
ECHO.>> %GBF_PathToBuildInfoCSFileTemp%
ECHO namespace FractaliaBuildInfo>> %GBF_PathToBuildInfoCSFileTemp%
ECHO {>> %GBF_PathToBuildInfoCSFileTemp%
ECHO    static public class BuildInfo>> %GBF_PathToBuildInfoCSFileTemp%
ECHO    {>> %GBF_PathToBuildInfoCSFileTemp%
ECHO       public const int MajorVersion= %FRCT_MajorVersion%;>> %GBF_PathToBuildInfoCSFileTemp%
ECHO       public const int MinorVersion= %FRCT_MinorVersion%;>> %GBF_PathToBuildInfoCSFileTemp%
REM ECHO       public const string SvnRevision = "%FRCT_SvnRevisionNr%";>> %GBF_PathToBuildInfoCSFileTemp%
REM ECHO       public const bool SvnOpenedFiles = %FRCT_SvnOpenState%;>> %GBF_PathToBuildInfoCSFileTemp%
ECHO       public const string BuildTime = "%GBF_DateTime%";>> %GBF_PathToBuildInfoCSFileTemp%
ECHO    }>> %GBF_PathToBuildInfoCSFileTemp%
ECHO }>> %GBF_PathToBuildInfoCSFileTemp%
ECHO.>> %GBF_PathToBuildInfoCSFileTemp%

IF NOT EXIST %GBF_PathToBuildInfoCSFileTemp% (
   ECHO Error: Unable to generate %GBF_PathToBuildInfoCSFileTemp% >&2
   GOTO LblGenerateBuildInfoFileError
)

DEL /F /Q %GBF_PathToBuildInfoCSFileFinal% >nul 2>&1
IF EXIST %GBF_PathToBuildInfoCSFileFinal% (
   ECHO Error: Unable to delete %GBF_PathToBuildInfoCSFileFinal% >&2
   GOTO LblGenerateBuildInfoFileError
)

COPY %GBF_PathToBuildInfoCSFileTemp% %GBF_PathToBuildInfoCSFileFinal% >nul

IF ERRORLEVEL 1 (
   ECHO Unable to generate %GBF_PathToBuildInfoCSFileFinal% >&2
   GOTO LblGenerateBuildInfoFileError
)

IF NOT EXIST %GBF_PathToBuildInfoCSFileFinal% (
   ECHO Error: Unable to generate %GBF_PathToBuildInfoCSFileFinal% >&2
   GOTO LblGenerateBuildInfoFileError
)

GOTO LblGenerateBuildInfoFileDone

:LblGenerateBuildInfoFileError
SET GBI_GenerateBuildInfoFileExitCode=1

:LblGenerateBuildInfoFileDone

EXIT /B %GBI_GenerateBuildInfoFileExitCode%
GOTO :EOF


