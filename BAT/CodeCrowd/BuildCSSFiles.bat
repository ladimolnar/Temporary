@ECHO OFF
IF "%useverbose%"=="1" ECHO ON
REM =================================================================
REM File BuildCSSFiles.BAT
REM Generates the CSS files running the CSSX files 
REM through the C preprocessor (CL.EXE).
REM =================================================================
SETLOCAL

SET CC_PathToScriptFolder=%~dp0
CALL %CC_PathToScriptFolder%\SetCoreEnvVars.bat
IF ERRORLEVEL 1 GOTO LblDoneWithError

SET CSS_PathToStyleSheetFolder=%CC_SVNTrunk%\CodeCrowd\CodeCrowd\CodeCrowd.Web\StyleSheet

IF "%1" == "/?" (CALL :SubHelp & GOTO LblExitOK)
IF "%1" == "/VSPrompt" (
   CALL "C:\Program Files (x86)\Microsoft Visual Studio 10.0\VC\vcvarsall.bat" x86
   IF ERRORLEVEL 1 (
      ECHO Error setting the Ms Visual Studio 2010 x86 tools environment. >&2
      GOTO LblDoneWithError
   )
)

CALL :SubProcessCSSXFile %CSS_PathToStyleSheetFolder%\CodeCrowd.cssx CodeCrowd.css
IF ERRORLEVEL 1 (
   ECHO Error processing CodeCrowd.cssx >&2
   GOTO LblDoneWithError
)

CALL :SubProcessCSSXFile %CSS_PathToStyleSheetFolder%\MenuTabItems.cssx MenuTabItems.css
IF ERRORLEVEL 1 (
   ECHO Error processing MenuTabItems.cssx >&2
   GOTO LblDoneWithError
)

CALL :SubProcessCSSXFile %CSS_PathToStyleSheetFolder%\AdminMenuTabItems.cssx AdminMenuTabItems.css
IF ERRORLEVEL 1 (
   ECHO Error processing AdminMenuTabItems.cssx >&2
   GOTO LblDoneWithError
)

CALL :SubProcessCSSXFile %CSS_PathToStyleSheetFolder%\MasterPages.cssx MasterPages.css
IF ERRORLEVEL 1 (
   ECHO Error processing MasterPages.cssx >&2
   GOTO LblDoneWithError
)

CALL :SubProcessCSSXFile %CSS_PathToStyleSheetFolder%\CodeCrowdAdmin.cssx CodeCrowdAdmin.css
IF ERRORLEVEL 1 (
   ECHO Error processing CodeCrowdAdmin.cssx >&2
   GOTO LblDoneWithError
)

:LblDoneOK
ECHO.
ECHO BuildCSSFiles ended successfully. CSSX files were processed.

:LblExitOK
SET CSS_ExitCode=0

:LblExit
cmd /C EXIT %CSS_ExitCode%
ENDLOCAL
GOTO :EOF

:LblDoneWithError
ECHO.>&2
ECHO BuildCSSFiles done with errors! >&2
SET CSS_ExitCode=1
GOTO LblExit

REM =================================================================
REM SubProcessCSSXFile
REM =================================================================
:SubProcessCSSXFile

SET CSS_PathToSourceCssx=%~1
SET CSS_SourceCssxFileName=%~nx1
SET CSS_PathToDestinationCss=%~dp1%~nx2
SET CSS_DestinationCssFileName=%~nx2

ECHO Process %CSS_SourceCssxFileName% -^> %CSS_DestinationCssFileName%

IF NOT EXIST "%CSS_PathToSourceCssx%" (
   ECHO Error: File %CSS_SourceCssxFileName% was not found>&2
   GOTO LblProcessScriptWithError
)

DEL /F /Q "%CSS_PathToDestinationCss%" >nul 2>&1
IF EXIST "%CSS_PathToDestinationCss%" (
   ECHO Error: Unable to delete %CSS_DestinationCssFileName% >&2
   GOTO LblDoneWithError
)

ECHO /* > "%CSS_PathToDestinationCss%"
ECHO ------------------------------------------------------------------------------ >> "%CSS_PathToDestinationCss%"
ECHO [auto-generated] >> "%CSS_PathToDestinationCss%"
ECHO     This file was generated by a tool (BuildCSSFiles.bat). >> "%CSS_PathToDestinationCss%"
ECHO     Changes to this file may cause incorrect behavior and will be lost if >> "%CSS_PathToDestinationCss%"
ECHO     the code is regenerated. >> "%CSS_PathToDestinationCss%"
ECHO [auto-generated] >> "%CSS_PathToDestinationCss%"
ECHO ------------------------------------------------------------------------------ >> "%CSS_PathToDestinationCss%"
ECHO */ >> "%CSS_PathToDestinationCss%"

REM Compile the source CSSX files into the actual CSS file that can be used by the web pages.
CL.EXE /nologo "%CSS_PathToSourceCssx%" /EP >> "%CSS_PathToDestinationCss%" 2>"%CC_Temp%\CLOutput.log"
IF ERRORLEVEL 1 (
   ECHO Error running CL.EXE. Make sure that you are running in a VS command prompt. >&2
   TYPE "%CC_Temp%\CLOutput.log" >&2
   GOTO LblProcessScriptWithError
)

GOTO :EOF

:LblProcessScriptWithError
cmd /c exit 1
goto :EOF


REM ==============================================================
REM Prints a help page to the output
REM ==============================================================
:SubHelp

ECHO.
ECHO BuildCSSFiles.BAT - Will generate the CSS files needed
ECHO                     by the web pages of the CodeCrowd site.
ECHO.
ECHO Usage: BuildCSSFiles.BAT  [/?] [/VSPrompt]
ECHO.    /?             Will display this help page.
ECHO     /VSPrompt      Will set the environment for using 
ECHO                    Ms Visual Studio 2010 x86 tools prior to running.
ECHO     This script will process the original CSSX files through
ECHO     a C preprocessor and generate the CSS files needed by the
ECHO     web pages of the CodeCrowd site.
ECHO.
ECHO Exit Code:
ECHO     0 - Script completed successfully.
ECHO     1 - There were errors completing the script.

GOTO :EOF
