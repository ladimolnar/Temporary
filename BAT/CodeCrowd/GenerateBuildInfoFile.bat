@ECHO OFF
IF "%useverbose%"=="1" ECHO ON
REM =====================================================================
REM FILE: GenerateBuildInfoFile.bat
REM This is the utility that generate a C# file that contains constants 
REM for the build info
REM =====================================================================

SET GBF_PathToScriptFolder=%~dp0
CALL %GBF_PathToScriptFolder%\SetCoreEnvVars.bat
IF ERRORLEVEL 1 GOTO LblError

SET GBF_ExitCode=0

SET GBF_ProjectName=%~1
SET GBF_IncludeBuildTime=%~2

IF "%GBF_ProjectName%"=="" (
   ECHO Invalid command line. Please specify the project name.>&2
   ECHO Example: GenerateBuildInfoFile.bat CodeCrowd>&2
   GOTO LblErrorExit
)

SET GBF_PathToBuildInfoCSFileTemp="%CC_Temp%\BuildInfo.cs"
SET GBF_PathToBuildInfoCSFileFinal="%CC_ProjectSources%\%GBF_ProjectName%\BuildInfo.cs"

CALL %GBF_PathToScriptFolder%\GetBuildInfo.bat %CC_SVNArea%
IF ERRORLEVEL 1 GOTO LblError

SET GBF_DateTime=%DATE% %TIME%

CALL :GenerateBuildInfoFile
IF ERRORLEVEL 1 GOTO LblError
GOTO LblDone

:LblError
ECHO Error: GenerateBuildInfoFile.bat was unable to generate the build info file: '%GBF_PathToBuildInfoCSFileFinal%'>&2

:LblErrorExit
SET GBF_ExitCode=1

:LblDone
EXIT /B %GBF_ExitCode%

GOTO :EOF


REM ==============================================================
REM Generates the file BuildInfo.cs
REM ==============================================================
:GenerateBuildInfoFile

SET GBF_GenerateBuildInfoFileExitCode=0

ECHO.
ECHO Generating %GBF_PathToBuildInfoCSFileFinal% 
ECHO.
ECHO Major Version: %CC_MajorVersion%
ECHO Minor Version: %CC_MinorVersion%
ECHO SVN Revision Nr: %CC_SvnRevisionNr%
ECHO SVN Open State: %CC_SvnOpenState%
IF /I "%GBF_IncludeBuildTime%"=="Yes" (ECHO Build DateTime: %GBF_DateTime%) ELSE (ECHO Build DateTime is EXCLUDED)

DEL /F /Q %GBF_PathToBuildInfoCSFileTemp% >nul 2>&1
IF EXIST %GBF_PathToBuildInfoCSFileTemp% (
   ECHO Error: Unable to delete %GBF_PathToBuildInfoCSFileTemp% >&2
   GOTO LblGenerateBuildInfoFileError
)

ECHO // File auto generated by the build system. See GenerateBuildInfoFile.bat> %GBF_PathToBuildInfoCSFileTemp%
ECHO // ATTENTION! Do not add to SVN. Do not change manually.>> %GBF_PathToBuildInfoCSFileTemp%
ECHO.>> %GBF_PathToBuildInfoCSFileTemp%
ECHO using System;>> %GBF_PathToBuildInfoCSFileTemp%
ECHO.>> %GBF_PathToBuildInfoCSFileTemp%
ECHO namespace CodeCrowdBuildInfo >> %GBF_PathToBuildInfoCSFileTemp%
ECHO {>> %GBF_PathToBuildInfoCSFileTemp%
ECHO    static public class BuildInfo>> %GBF_PathToBuildInfoCSFileTemp%
ECHO    {>> %GBF_PathToBuildInfoCSFileTemp%
ECHO       public const int MajorVersion= %CC_MajorVersion%;>> %GBF_PathToBuildInfoCSFileTemp%
ECHO       public const int MinorVersion= %CC_MinorVersion%;>> %GBF_PathToBuildInfoCSFileTemp%
ECHO       public const string SvnRevision = "%CC_SvnRevisionNr%";>> %GBF_PathToBuildInfoCSFileTemp%
ECHO       public const bool SvnOpenedFiles = %CC_SvnOpenState%;>> %GBF_PathToBuildInfoCSFileTemp%
IF /I "%GBF_IncludeBuildTime%"=="Yes" (ECHO       public const string BuildTime = "%GBF_DateTime%";>> %GBF_PathToBuildInfoCSFileTemp%)
ECHO    }>> %GBF_PathToBuildInfoCSFileTemp%
ECHO }>> %GBF_PathToBuildInfoCSFileTemp%
ECHO.>> %GBF_PathToBuildInfoCSFileTemp%

IF NOT EXIST %GBF_PathToBuildInfoCSFileTemp% (
   ECHO Error: Unable to generate %GBF_PathToBuildInfoCSFileTemp% >&2
   GOTO LblGenerateBuildInfoFileError
)

DEL /F /Q %GBF_PathToBuildInfoCSFileFinal% >nul 2>&1
IF EXIST %GBF_PathToBuildInfoCSFileFinal% (
   ECHO Error: Unable to delete %GBF_PathToBuildInfoCSFileFinal% >&2
   GOTO LblGenerateBuildInfoFileError
)

COPY %GBF_PathToBuildInfoCSFileTemp% %GBF_PathToBuildInfoCSFileFinal% >nul

IF ERRORLEVEL 1 (
   ECHO Unable to generate %GBF_PathToBuildInfoCSFileFinal% >&2
   GOTO LblGenerateBuildInfoFileError
)

IF NOT EXIST %GBF_PathToBuildInfoCSFileFinal% (
   ECHO Error: Unable to generate %GBF_PathToBuildInfoCSFileFinal% >&2
   GOTO LblGenerateBuildInfoFileError
)

SET GBI_GenerateBuildInfoFileExitCode=0

GOTO LblGenerateBuildInfoFileDone

:LblGenerateBuildInfoFileError
SET GBI_GenerateBuildInfoFileExitCode=1

:LblGenerateBuildInfoFileDone

EXIT /B %GBI_GenerateBuildInfoFileExitCode%
GOTO :EOF


